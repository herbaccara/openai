import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.7.22' apply false
    id "org.jetbrains.kotlin.kapt" version "1.7.22" apply false
    id "com.google.devtools.ksp" version "1.7.22-1.0.8" apply false
    id "org.jmailen.kotlinter" version "3.13.0"
}

allprojects {
    version = "0.0.3"
    group = 'herbaccara'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        google()
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }

    apply plugin: "org.jetbrains.kotlin.jvm"
    tasks.withType(KotlinCompile) {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    apply plugin: "org.jmailen.kotlinter"
    kotlinter {
        disabledRules = ["no-wildcard-imports", "enum-entry-name-case"]
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.3'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
    }
}